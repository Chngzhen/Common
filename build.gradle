import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version "$springBootVersion" apply false
}

version '1.0.0'

allprojects {

    group 'pfu.common'

    repositories {
        mavenLocal()
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        mavenCentral()
    }

}

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'application'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }

        dependencies {
            dependency "cglib:cglib-nodep:$cglibNodepVersion"
            dependency "com.alibaba:fastjson:$fastJsonVersion"
            dependency "com.belerweb:pinyin4j:$pinyin4jVersion"
            dependency "com.github.xiaoymin:knife4j-spring-boot-starter:$knife4jStarterVersion"
            dependency "javax.validation:validation-api:$javaxValidationVersion"
            dependency "org.slf4j:slf4j-api:$slf4jApiVersion"
            dependency "org.aspectj:aspectjweaver:$aspectjWeaverVersion"
            dependency "org.hibernate.validator:hibernate-validator:$hibernateValidatorVersion"
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    test {
        useJUnitPlatform()
    }

    jar {
        // 配置文件不打入JAR包
        excludes = ["application.yml", "bootstrap.yml"]
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

}